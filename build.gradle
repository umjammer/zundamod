plugins {
	id 'fabric-loom' version '1.4-SNAPSHOT'
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven {
		url = uri("https://maven.pkg.github.com/umjammer/*")
		credentials {
			username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
			password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
		}
	}
	maven {
		url = uri("https://jitpack.io")
	}
	maven { url "https://maven.terraformersmc.com/releases/" } // for modmenu
}

configurations {
	dependencyJars
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Uncomment the following line to enable the deprecated Fabric API modules. 
	// These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.

	// fabric-loom provides modmenu 7.0.0 which doesn't work with mc 1.20.4, so specifying the newest version.
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	// modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"
	implementation ("vavi:vavi-speech2:0.0.9") {
		exclude group: "org.jvoicexml", module: "freetts_jsapi10"
		exclude group: "vavi", module: "vavi-util-screenscraping"
		exclude group: "vavi", module: "vavi-sound-nda"
		exclude group: "vavi", module: "vavi-speech"
		exclude group: "sen", module: "sen"
		exclude group: "com.github.umjammer", module: "vavi-commons-sandbox"
		exclude group: "com.github.umjammer.vavi-commons", module: "vavi-instrumentation"
		exclude group: "org.ow2.asm", module: "asm"
	}
	dependencyJars ("vavi:vavi-speech2:0.0.9") { // TODO how to copy this from implementation or use implementation w/o error
		exclude group: "org.jvoicexml", module: "freetts_jsapi10"
		exclude group: "vavi", module: "vavi-util-screenscraping"
		exclude group: "vavi", module: "vavi-sound-nda"
		exclude group: "vavi", module: "vavi-speech"
		exclude group: "sen", module: "sen"
		exclude group: "com.github.umjammer", module: "vavi-commons-sandbox"
		exclude group: "com.github.umjammer.vavi-commons", module: "vavi-instrumentation"
		exclude group: "org.ow2.asm", module: "asm"
	}
	// TODO HEY! HEY! HEY! Mr. fabric-loom, make include work recursively. my dependency jars are over 100!
	configurations.dependencyJars.incoming.resolutionResult.allDependencies
			.forEach { include(it.getRequested().toString())}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

assemble {
	version += "-${project.minecraft_version}-release"
}
